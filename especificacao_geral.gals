#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = String
Parser = LL
#RegularDefinitions
mai: [A-Z]
min: [a-z]
let: {mai} | {min}
dig: [0-9]
digex: [1-9]


#Tokens
//CONSTANTES
id: {let} ( _? ( {let} | {dig} ) ) *
cint: {dig}+ | {dig}* {digex} d {digex} {dig}*
cfloat: {dig}* \. ( {dig}+ | {dig}* {digex} d {digex} {dig}* )
cchar: \\ ( n | s | t )
cstring: \" [^ \" \n \\ ]* \"

//PALAVRAS RESERVADAS
pr_boolean = id: "boolean"
pr_break = id: "break"
pr_char = id: "char"
pr_do = id: "do"
pr_else = id: "else"
pr_end = id: "end"
pr_false = id: "false"
pr_float = id: "float"
pr_fun = id: "fun"
pr_if = id: "if"
pr_int = id: "int"
pr_main = id: "main"
pr_print = id: "print"
pr_println = id: "println"
pr_readln = id: "readln"
pr_string = id: "string"
pr_true = id: "true"
pr_val = id: "val"
pr_var = id: "var"
pr_while = id: "while"

//SIMBOLOS ESPECIAIS
":"
","
";"
"="
"("
")"
"{"
"}"
"=="
"!="
"<"
">"
"+"
"-"
"*"
"/"
"%"
"&&"
"||"
"!"
 
//FORMATACAO
:[\s\t\n]

//COMENTARIOS
:! # .* 
:! \[ \n [^\[ \] ]* \n \]
#NonTerminals
<geral>
<lista_instrucoes>
<lista_instrucoes1>
<declaracao>
<dec_consts>
<dec_vars>
<tipo_dec>
<tipo_dec1>

<lista_ids>
<lista_ids1>
<tipo>
<valor>
<comando>

<atribuicao>

<entrada>
<cstring_opc>

<saida>
<lista_exp>
<lista_exp1>

<selecao>
<else>
<lista_cmd>
<lista_cmd1>

<repeticao>

<expressao>
<expressao_>
<elemento>
<relacional>
<relacional_>
<operador_relacional>
<aritmetica>
<aritmetica_>
<termo>
<termo_>
<fator>
#Grammar
<geral> ::= #15 pr_fun pr_main "{" <lista_instrucoes> "}" #16 ;

<lista_instrucoes> ::= <declaracao> <lista_instrucoes1> ;
<lista_instrucoes1> ::= <lista_instrucoes> | î ;
<declaracao> ::= <dec_consts> | <dec_vars> | <comando> ;
<dec_consts> ::= pr_val <lista_ids> <tipo_dec> ;
<dec_vars> ::= pr_var <lista_ids> <tipo_dec> ;
<tipo_dec> ::= ":" <tipo> #30 #31 <tipo_dec1> | "=" <valor> ";" ;
<tipo_dec1> ::= ";" | "=" <valor> ";" ;

<lista_ids> ::= id #32 <lista_ids1> ;
<lista_ids1> ::= î | "," <lista_ids> ;
<tipo> ::= pr_int | pr_float | pr_char | pr_string | pr_boolean ;
<valor> ::= cint | cfloat | cchar | cstring | pr_true | pr_false ;
<comando> ::= <atribuicao> | <entrada> | <saida> | <selecao> | <repeticao> | pr_break ";" ;

<atribuicao> ::= id #32 "=" <expressao> #34 ";" ;

<entrada> ::= pr_readln "(" <cstring_opc> <lista_ids> #35 ")" ";" ;
<cstring_opc> ::= cstring #22 #14 "," | î ;

<saida> ::= pr_print "(" <lista_exp> ")" ";" | pr_println "(" <lista_exp> ")" #17 ";" ;
<lista_exp> ::= <expressao> #14 <lista_exp1> ;
<lista_exp1> ::= î | "," <lista_exp> ;

<selecao> ::= pr_if "(" <expressao> ")" #24 <lista_cmd> <else> pr_end #26 ";" ;
<else> ::= pr_else #25 <lista_cmd> | î ;
<lista_cmd> ::= <comando> <lista_cmd1> ;
<lista_cmd1> ::= î | <lista_cmd> ;

<repeticao> ::= #27 pr_do <lista_cmd> pr_while "(" <expressao> ")" #28 pr_end ";" ;

<expressao> ::= <elemento> <expressao_> ;
<expressao_> ::= î | "&&" <elemento> #18 <expressao_> | "||" <elemento> #19 <expressao_> ;
<elemento> ::= <relacional> | pr_true #11 | pr_false #12 | "!" <elemento> #13 ;
<relacional> ::= <aritmetica> <relacional_> ;
<relacional_> ::= î | <operador_relacional> #9 <aritmetica> #10 ;
<operador_relacional> ::= "==" | "!=" | "<" | ">" ;
<aritmetica> ::= <termo> <aritmetica_> ;
<aritmetica_> ::= î | "+" <termo> #1 <aritmetica_> | "-" <termo> #2 <aritmetica_> ;
<termo> ::= <fator> <termo_> ;
<termo_> ::= î | "*" <fator> #3 <termo_> | "/" <fator> #4 <termo_> | "%" <fator> #20 <termo_> ;
<fator> ::= id #33 | cint #5 | cfloat #6 | cchar #21 | cstring #22 | "(" <expressao> ")" | "+" <fator> #7 | "-" <fator> #8 ;
